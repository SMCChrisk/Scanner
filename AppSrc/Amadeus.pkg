// Amadeus specific structures and processes

Define AMADEUS_DELIMITER for ';'

Struct tBLKMessageHeader // Block Message Header Segment
    String  sAIRVER // AIR version Number: Valid versions:204, 205, 206, 207 (char 3)
    String  sAIROPT // AIR option Number
    String  sTRANNUM // Transmission Number
    String  sNETID // Network Identifier (2 char)
    String  sADDR  // Identifier of terminal generating AIR (8 char)
    String  sBLKNUM // Block Number transmitted (char 3)
    String  sTOTBLK // Total Number of blocks containing AIR (char 3)
End_Struct

Struct tAIRHeader // AMD - AIR Record Header
    String  sMSGTAG   // Message Tag (AMD) (char 3)
    String  sRETRANS  // Retransmitted Record Indicator (R=Retransmitted, blanks If not used) (char 1)
    String  sDOM      // Day of month numeric (Num 2)
    String  sAIRSEQ   // AIR sequence Number (Num 8)
    Integer iAIRSEC   // AIR section Number (Num 1..2) has '/' terminator between fields
    Integer iAIRTOTAL // AIR Total Number (Num 1..2) 
    String  sACTTAG   // Action tag (Constant value of 'VOID' or ‘RSTD’ or ‘TRFX’ or blanks) (char 4)
    String  sDATE     // Date when voided (DDMMM) (char 5)
    String  sAGTSIN   // Agent Sign that voided AIR (Sine = Initials and Duty Code) (system void = "****") (char 4)
    // CR
    String  sNETID    // The network identifier (Char 2)
    String  sADDRTKT  // Identifier of the terminal requesting the ticket (Num 8)
    String  sNETIDBOS // The network identifier (for back offices purposes) (Char 2)
    Integer iADDRTKTBOS // The identifier of the terminal activating the a.I.R. (for back offices purposes) (Num 8)
    String  sIDENTBOS   // Back Office a.I.R. destination office (Char 9)
    String  sBOSTYPE    // a.I.R. destination type: BOS type as to list defined in A02AIRT4. (Char 3)
    // CR
    String  sRECLOC     // Record locator of PNR in GC (If temporary ticketing PNR then constant value of 'NOPNR') (Char 6)
    String  sPNRENV     // PNR envelope Number (If temporary ticketing PNR, then constant value of 'blanks') (Num 3)
    String  sTOTPSG     // Total Number of passengers in PNR (Num 2)
    String  siAIRPSG    // Total Number of passengers in AIR (NB: Number of RPA lines for type ‘RA’) (Num 2)
    String  sNHPNR      // Non Homogeneous PNR Indicator (N = Non Homogeneous PNR) (Char 1)
    String  sIDBKG      // Amadeus Office ID of booking agency.For a description of the Amadeus Office Id please see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
    String  sBKGAGCY    // IATA Number of booking agency (Char 8)
    String  sIDFIRST    // Amadeus Office ID of PNR first Owner. For a description of the Amadeus Office Id please see Amadeus Office Identification (A02OFP3204) (Char 9)
    String  sOWNAGY     // IATA Number of PNR first owner (Char 8)
    String  sIDENTCO    // Amadeus Office ID of current owner of PNR.For a description of the Amadeus Office Id please see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
    String  sCURAGCY    // IATA Number of current agency (Char 8)
    String  sIDENTT     // Amadeus office ID of the ticketing agency. For more information see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
    String  sTKTAGCY    // IATA Number of Ticketing Agency (Char 8)
    String  sOFFID      // Office Subdivision identifier (Char 2)
    String  sCPNID      // Coupon Identifier (for a detailed description see A02-3277 Satellite Ticket Printing) (Char 15)
    String  sIDENTT2    // Amadeus office ID of STP Site 1 For more information see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
    String  sTKTAGCY2   // IATA Number of STP Site 1 (Char 8)
    String  sOFFID2     // STP 1 Office Subdivision identifier (Char 2)
    String  sCPNID2     // Coupon Identifier (for a detailed description see A02-3277 Satellite Ticket Printing) (Char 15)
    String  sIDENTT3    // Amadeus office ID of STP Site 2 For more information see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
    String  sTKTAGCY3   // IATA Number of STP Site 2 (Char 8)
    String  sOFFID3     // STP 2 Office Subdivision identifier (Char 2)
    String  sCPNID3     // Coupon Identifier (for a detailed description see A02-3277 Satellite Ticket Printing) (Char 15)
    String  sIDENTT4    // Amadeus office ID of STP Site 3 For more information see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
    String  sTKTAGCY4   // IATA Number of STP Site 3 (Char 8)
    String  sOFFID4     // STP 3 Office Subdivision identifier (Char 2)
    String  sCPNID4     // Coupon Identifier (for a detailed description see A02-3277 Satellite Ticket Printing) (Char 15)
    String  sIDENTT5    // Amadeus office ID of STP Site 4 For more information see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
    String  sTKTAGCY5   // IATA Number of STP Site 4 (Char 8)
    String  sOFFID5     // STP 4 Office Subdivision identifier (Char 2)
    String  sCPNID5     // Coupon Identifier (for a detailed description see A02-3277 Satellite Ticket Printing) (Char 15)
    String  sACCTNUM    // Agency Client Account Number (Char 10)
    String  sORDNUM     // Order Number (Char 10)
    String  sERSP       // ERSP of booking agency (Num 8)
//    String  sNSPNR      // National System PNR. (Char 28)
    String  sNSAIR      // National System Airline Code (Char 3)
    String  sNSRECLOC   // National System PNR record locator (Char 7)
End_Struct

Struct tAmadeusAIR
    tBLKMessageHeader BLKMessageHeader
    tAIRHeader AIRHeader
End_Struct

Function ParseBLKHeader Global String[] sInputRecord tAmadeusAIR ByRef AmadeusAIR Returns Boolean
    Boolean bSuccess
    String[] saRecordFields
    

    Move (StrSplitToArray(sInputRecord[0], AMADEUS_DELIMITER)) to saRecordFields
    Move (SizeOfArray(saRecordFields) = 7) to bSuccess
    If (bSuccess) Begin
        Move (right(saRecordFields[0], 3)) to AmadeusAIR.BLKMessageHeader.sAIRVER
        Move (saRecordFields[1])           to AmadeusAIR.BLKMessageHeader.sAIROPT
        Move (saRecordFields[4])           to AmadeusAIR.BLKMessageHeader.sTRANNUM
        Move (left(saRecordFields[5], 2))  to AmadeusAIR.BLKMessageHeader.sNETID
        Move (mid(saRecordFields[5], 8, 3)) to AmadeusAIR.BLKMessageHeader.sADDR
        Move (left(saRecordFields[6], 3))   to AmadeusAIR.BLKMessageHeader.sBLKNUM
        Move (mid(saRecordFields[6], 3, 4)) to AmadeusAIR.BLKMessageHeader.sTOTBLK
    End

    Function_Return (bSuccess)
End_Function

Function ParseAIRHeader Global String[] sInputRecord tAmadeusAIR ByRef AmadeusAIR Returns Boolean
    Boolean bSuccess
    String[] saTemp
    String[] saRecordFields

    Move (StrSplitToArray(sInputRecord[0], AMADEUS_DELIMITER)) to saRecordFields // AMD record
    Move (SizeOfArray(saRecordFields) >= 3) to bSuccess // May have 3 or 4
    If (bSuccess) Begin
        Move (left(saRecordFields[0], 3))   to AmadeusAIR.AIRHeader.sMSGTAG
        Move (mid(saRecordFields[0], 1, 4)) to AmadeusAIR.AIRHeader.sRETRANS
        Move (mid(saRecordFields[0], 2, 6)) to AmadeusAIR.AIRHeader.sDOM
        Move (mid(saRecordFields[0], 8, 6)) to AmadeusAIR.AIRHeader.sAIRSEQ
        Move (StrSplitToArray(saRecordFields[1], '/')) to saTemp
        Move (saTemp[0])                    to AmadeusAIR.AIRHeader.iAIRSEC
        Move (saTemp[1])                    to AmadeusAIR.AIRHeader.iAIRTOTAL
        Move (left(saRecordFields[2], 4))   to AmadeusAIR.AIRHeader.sACTTAG
        Move (mid(saRecordFields[2], 5, 5)) to AmadeusAIR.AIRHeader.sDATE
        If (SizeOfArray(saRecordFields) > 3) ;
            Move (saRecordFields[3])        to AmadeusAIR.AIRHeader.sAGTSIN
    End

    Move (StrSplitToArray(sInputRecord[1], AMADEUS_DELIMITER)) to saRecordFields // Next line
    Move (SizeOfArray(saRecordFields) >= 2) to bSuccess // May have 3
    If (bSuccess) Begin
        Move (left(saRecordFields[0], 2))   to AmadeusAIR.AIRHeader.sNETID
        Move (Mid(saRecordFields[0], 8, 3)) to AmadeusAIR.AIRHeader.sADDRTKT 
        Move (saRecordFields[1])            to AmadeusAIR.AIRHeader.sIDENTBOS
        If (SizeOfArray(saRecordFields) > 2) ;
            Move (saRecordFields[2])        to AmadeusAIR.AIRHeader.sBOSTYPE
    End

    Move (StrSplitToArray(sInputRecord[2], AMADEUS_DELIMITER)) to saRecordFields // MUC1A line
    Move (SizeOfArray(saRecordFields) = 31) to bSuccess // Should have 31
    If (bSuccess) Begin
        Move (Mid(saRecordFields[00], 06, 07)) to AmadeusAIR.AIRHeader.sRECLOC     // Record locator of PNR in GC (If temporary ticketing PNR then constant value of 'NOPNR') (Char 6)
        Move (Mid(saRecordFields[00], 03, 13)) to AmadeusAIR.AIRHeader.sPNRENV     // PNR envelope Number (If temporary ticketing PNR, then constant value of 'blanks') (Num 3)
        Move (Mid(saRecordFields[01], 02, 01)) to AmadeusAIR.AIRHeader.sTOTPSG     // Total Number of passengers in PNR (Num 2)
        Move (Mid(saRecordFields[01], 02, 03)) to AmadeusAIR.AIRHeader.siAIRPSG    // Total Number of passengers in AIR (NB: Number of RPA lines for type ‘RA’) (Num 2)
        Move (Mid(saRecordFields[01], 01, 05)) to AmadeusAIR.AIRHeader.sNHPNR      // Non Homogeneous PNR Indicator (N = Non Homogeneous PNR) (Char 1)
        Move (saRecordFields[02])              to AmadeusAIR.AIRHeader.sIDBKG      // Amadeus Office ID of booking agency.For a description of the Amadeus Office Id please see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
        Move (saRecordFields[03])              to AmadeusAIR.AIRHeader.sBKGAGCY    // IATA Number of booking agency (Char 8)
        Move (saRecordFields[04])              to AmadeusAIR.AIRHeader.sIDFIRST    // Amadeus Office ID of PNR first Owner. For a description of the Amadeus Office Id please see Amadeus Office Identification (A02OFP3204) (Char 9)
        Move (saRecordFields[05])              to AmadeusAIR.AIRHeader.sOWNAGY     // IATA Number of PNR first owner (Char 8)
        Move (saRecordFields[06])              to AmadeusAIR.AIRHeader.sIDENTCO    // Amadeus Office ID of current owner of PNR.For a description of the Amadeus Office Id please see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
        Move (saRecordFields[07])              to AmadeusAIR.AIRHeader.sCURAGCY    // IATA Number of current agency (Char 8)
        Move (saRecordFields[08])              to AmadeusAIR.AIRHeader.sIDENTT     // Amadeus office ID of the ticketing agency. For more information see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
        Move (saRecordFields[09])              to AmadeusAIR.AIRHeader.sTKTAGCY    // IATA Number of Ticketing Agency (Char 8)
        Move (saRecordFields[10])              to AmadeusAIR.AIRHeader.sOFFID      // Office Subdivision identifier (Char 2)
        Move (saRecordFields[11])              to AmadeusAIR.AIRHeader.sCPNID      // Coupon Identifier (for a detailed description see A02-3277 Satellite Ticket Printing) (Char 15)
        Move (saRecordFields[12]) to AmadeusAIR.AIRHeader.sIDENTT2    // Amadeus office ID of STP Site 1 For more information see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
        Move (saRecordFields[13]) to AmadeusAIR.AIRHeader.sTKTAGCY2   // IATA Number of STP Site 1 (Char 8)
        Move (saRecordFields[14]) to AmadeusAIR.AIRHeader.sOFFID2     // STP 1 Office Subdivision identifier (Char 2)
        Move (saRecordFields[15]) to AmadeusAIR.AIRHeader.sCPNID2     // Coupon Identifier (for a detailed description see A02-3277 Satellite Ticket Printing) (Char 15)
        Move (saRecordFields[16]) to AmadeusAIR.AIRHeader.sIDENTT3    // Amadeus office ID of STP Site 2 For more information see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
        Move (saRecordFields[17]) to AmadeusAIR.AIRHeader.sTKTAGCY3   // IATA Number of STP Site 2 (Char 8)
        Move (saRecordFields[18]) to AmadeusAIR.AIRHeader.sOFFID3     // STP 2 Office Subdivision identifier (Char 2)
        Move (saRecordFields[19]) to AmadeusAIR.AIRHeader.sCPNID3     // Coupon Identifier (for a detailed description see A02-3277 Satellite Ticket Printing) (Char 15)
        Move (saRecordFields[20]) to AmadeusAIR.AIRHeader.sIDENTT4    // Amadeus office ID of STP Site 3 For more information see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
        Move (saRecordFields[21]) to AmadeusAIR.AIRHeader.sTKTAGCY4   // IATA Number of STP Site 3 (Char 8)
        Move (saRecordFields[22]) to AmadeusAIR.AIRHeader.sOFFID4     // STP 3 Office Subdivision identifier (Char 2)
        Move (saRecordFields[23]) to AmadeusAIR.AIRHeader.sCPNID4     // Coupon Identifier (for a detailed description see A02-3277 Satellite Ticket Printing) (Char 15)
        Move (saRecordFields[24]) to AmadeusAIR.AIRHeader.sIDENTT5    // Amadeus office ID of STP Site 4 For more information see Amadeus Office Identification (a-02-3204-AMD) (Char 9)
        Move (saRecordFields[25]) to AmadeusAIR.AIRHeader.sTKTAGCY5   // IATA Number of STP Site 4 (Char 8)
        Move (saRecordFields[26]) to AmadeusAIR.AIRHeader.sOFFID5     // STP 4 Office Subdivision identifier (Char 2)
        Move (saRecordFields[27]) to AmadeusAIR.AIRHeader.sCPNID5     // Coupon Identifier (for a detailed description see A02-3277 Satellite Ticket Printing) (Char 15)
        Move (saRecordFields[28]) to AmadeusAIR.AIRHeader.sACCTNUM    // Agency Client Account Number (Char 10)
        Move (saRecordFields[29]) to AmadeusAIR.AIRHeader.sORDNUM     // Order Number (Char 10)
//        Move (saRecordFields[30]) to AmadeusAIR.AIRHeader.sERSP       // ERSP of booking agency (Num 8)
        Move (Mid(saRecordFields[30], 03, 01)) to AmadeusAIR.AIRHeader.sNSAIR      // National System Airline Code (Char 3)
        Move (Mid(saRecordFields[30], 07, 04)) to AmadeusAIR.AIRHeader.sNSRECLOC   // National System PNR record locator (Char 7)
    End
    
    Function_Return (bSuccess)
End_Function

Function LocateAIRSection Global String[] sInputRecord String sMatchValue Returns Integer
    Integer iRtnVal iRow iMax iMatchLength

    Move (-1) to iRtnVal
    Move (Length(sMatchValue)) to iMatchLength
    Move (SizeOfArray(sInputRecord)) to iMax
    For iRow from 0 to (iMax - 1)
        If (Left(sInputRecord[iRow], iMatchLength) = sMatchValue) Begin
            Move iRow to iRtnVal
            Break // out of loop
        End
    Loop
    Function_Return iRtnVal // A -1 will mean it was not found since it is zero indexed into the array
End_Function


Function ParseAmadeusAIR Global String[] sInputRecord Returns tAmadeusAIR
    // Primary function to parse a raw Amadadeus AIR text into a structured Amadeus AIR record
    tAmadeusAIR AmadeusAIR
    String[] sBLKHeader
    String[] sAIRHeader
    String[] sItinerary
    String[][] saInvoice
    String[] sFooter
    Integer iRow iMax
    Integer iBLKHeaderPtr iAIRHeaderPtr iItineraryPtr iInvoicePtr iFooterPtr
    Boolean bSuccess
    
    // Scan the line based text record to identify the main parts of an AIR.
    // Block header, AIR Header, Itinerary, Invoice (possibly multiple sets), footer
    Move (LocateAIRSection(sInputRecord, "AIR-BLK")) to iBLKHeaderPtr
    Move (LocateAIRSection(sInputRecord, "AMD")) to iAIRHeaderPtr
    Move (LocateAIRSection(sInputRecord, "A-")) to iItineraryPtr
    If (iItineraryPtr < 0) ;
        Move (LocateAIRSection(sInputRecord, "B-")) to iItineraryPtr // May be missing the 'A-' so try 'B-'
    If (iItineraryPtr < 0) ;
        Move (LocateAIRSection(sInputRecord, "I-")) to iItineraryPtr // May be missing the 'A-' and 'B-' so try 'I-' (MA type void records)

    // Move only those sections into the subrecord arrays
    If (iBLKHeaderPtr > -1) Begin
        For iRow from iBLKHeaderPtr to (iAIRHeaderPtr - 1)
            Move sInputRecord[iRow] to sBLKHeader[-1] // Append to the subrecord array
        Loop
    End

    If (iAIRHeaderPtr > -1) Begin
        For iRow from iAIRHeaderPtr to (iItineraryPtr - 1)
            Move sInputRecord[iRow] to sAIRHeader[-1] // Append to the subrecord array
        Loop
    End

    Move (ParseBLKHeader(sBLKHeader, &AmadeusAIR)) to bSuccess
    Move (ParseAIRHeader(sAIRHeader, &AmadeusAIR)) to bSuccess
    
    Function_Return (AmadeusAIR)
End_Function

